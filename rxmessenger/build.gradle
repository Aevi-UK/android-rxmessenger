plugins {
    id "com.jfrog.bintray" version "1.7.3"
}
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version = "${apiVersion}"
group = 'com.aevi.android'
ext.artifactId = 'rxmessenger'

apply from: '../java-provided.gradle'
apply from: '../publish-repo.gradle'
apply from: '../jar-publisher.gradle'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation domainDependencies.gson
    implementation domainDependencies.rxJava
    implementation domainDependencies.okio
    implementation domainDependencies.okHttp
    implementation domainDependencies.nanoHttpd
    implementation domainDependencies.nanoWebsockets

    compileOnly domainDependencies.androidMockable
    implementation domainDependencies.archLifecycle
    implementation domainDependencies.archLifecycleJava8

    testCompile domainDependencies.androidMockable
    testCompile testDependencies.junit
    testCompile testDependencies.mockito
    testCompile testDependencies.assertJ
    testCompile testDependencies.robolectric
    testCompile testDependencies.hamcrest

    // FIXME - Latest version of Android Studio seems to require these to run this projects unit tests but they are not required for gradle externally!?
    testCompile "org.ow2.asm:asm:6.2.1"
    testCompile "org.ow2.asm:asm-commons:6.2.1"
}

javadoc {
    source = sourceSets.main.allJava
    destinationDir = file("../javadocs/rxmessenger")
    classpath = files(sourceSets.main.compileClasspath, sourceSets.main.runtimeClasspath)
    title = "AEVI Android RxMessenger"
    options.memberLevel = JavadocMemberLevel.PUBLIC
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

def siteUrl = 'https://github.com/Aevi-UK/android-rxmessenger'
def gitUrl = 'https://github.com/Aevi-UK/android-rxmessenger.git'

bintray {
    publications = ['apiJar']
    override = true

    pkg {
        dryRun = false
        publish = true
        repo = 'aevi-uk'
        name = 'rxmessenger'
        userOrg = 'aevi'
        licenses = ['Apache-2.0']
        labels = ['rxjava', 'reactivex', 'android']
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/Aevi-UK/android-rxmessenger/issues'
        vcsUrl = gitUrl
        publicDownloadNumbers = true
    }
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
    bintray.user = project.bintrayUser
    bintray.key = project.bintrayKey
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'RxMessenger'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'brettwold'
                        name 'Brett Cherrington'
                        email 'brett@annalytics.co.uk'
                    }
                    developer {
                        id 'andreas-borglin'
                        name 'Andreas Borglin'
                        email 'andreas@jakoon.co.uk'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}