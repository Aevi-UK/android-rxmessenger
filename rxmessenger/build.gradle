plugins {
    id "com.jfrog.bintray" version "1.7.3"
}
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

apply from: '../java-provided.gradle'
apply from: '../publish-repo.gradle'
apply from: '../jar-publisher.gradle'

version = "${apiVersion}"
group = 'com.aevi.android'
ext.artifactId = 'rxmessenger'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    compile domainDependencies.gson
    compile domainDependencies.rxJava
    compile domainDependencies.okio
    provided domainDependencies.androidMockable
    provided domainDependencies.archLifecycle
    provided domainDependencies.archLifecycleJava8

    testCompile domainDependencies.androidMockable
    testCompile testDependencies.junit
    testCompile testDependencies.mockito
    testCompile testDependencies.assertJ
    testCompile testDependencies.robolectric
    testCompile testDependencies.robolectricannotations
}

javadoc {
    source = sourceSets.main.allJava
    destinationDir = file("../javadocs/rxmessenger")
    classpath = files(configurations.compile, configurations.provided)
    title = "Android RxMessenger"
    options.memberLevel = JavadocMemberLevel.PUBLIC
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

def siteUrl = 'https://github.com/Aevi-UK/android-rx-messenger'
def gitUrl = 'https://github.com/Aevi-UK/android-rx-messenger.git'

bintray {
    configurations = ['archives']

    pkg {
        repo = 'aevi-uk'
        name = 'rx-messenger'
        userOrg = 'aevi'
        licenses = ['Apache-2.0']
        labels = ['rxjava', 'reactivex', 'android']
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/Aevi-UK/android-rx-messenger/issues'
        vcsUrl = gitUrl
        publicDownloadNumbers = true
    }
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
    bintray.user = project.bintrayUser
    bintray.key = project.bintrayKey
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'RxMessenger'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'brettwold'
                        name 'Brett Cherrington'
                        email 'brett@annalytics.co.uk'
                    }
                    developer {
                        id 'andreas-borglin'
                        name 'Andreas Borglin'
                        email 'andreas@jakoon.co.uk'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}